buildscript {
    ext {
        springBootVersion = '2.1.0.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('io.swagger:swagger-codegen:2.3.1')
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.tam.events'
project.archivesBaseName = 'tam-server'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        name 'local repository'
        url "${rootProject.projectDir}/gradle/repository"
    }
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

configurations {
    generatedCompile
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-mongodb")
    compile('org.springframework.boot:spring-boot-starter-data-rest')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-mail:2.1.5.RELEASE')
    compile('io.springfox:springfox-swagger2:2.8.0')
    compile('io.springfox:springfox-swagger-ui:2.8.0')
    compile('io.jsonwebtoken:jjwt:0.9.0')
    compile('org.yaml:snakeyaml:1.23')
    compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
    compile('com.fasterxml.jackson.core:jackson-core')
    compile('com.fasterxml.jackson.core:jackson-databind')
    compile('com.fasterxml.jackson.dataformat:jackson-dataformat-csv')
    compile('javax.xml.bind:jaxb-api:2.3.0')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-mail')
    compile('org.passay:passay:1.4.0')
    testCompile('org.springframework.boot:spring-boot-starter-test')

    annotationProcessor("org.projectlombok:lombok:1.18.6")
    compileOnly("org.projectlombok:lombok:1.18.6")

    generatedCompile('org.springframework.boot:spring-boot-starter-data-rest')
    generatedCompile('io.springfox:springfox-swagger2:2.8.0')
    generatedCompile('io.springfox:springfox-swagger-ui:2.8.0')

}

// Code for API generation based on the API definition
import io.swagger.codegen.DefaultGenerator
import io.swagger.codegen.config.CodegenConfigurator

def apiDefinitionSourceFile = './api-definition.inc.yml'
def apiTargetFolder = 'src/generated/java'

task generateApi {
    inputs.file("$projectDir/$apiDefinitionSourceFile")
    outputs.dir("$projectDir/$apiTargetFolder")
    doLast {
        def config = new CodegenConfigurator()
        config.setInputSpec("$projectDir/$apiDefinitionSourceFile")
        config.setOutputDir("$projectDir")
        config.setLang('spring')
        config.setAdditionalProperties([
                'interfaceOnly'  : 'true',
                'apiPackage'     : 'com.tam.api',
                'modelPackage'   : 'com.tam.model',
                'modelNameSuffix': 'Resource',
                'java8'          : 'true',
                'sourceFolder'   : apiTargetFolder
        ])
        new DefaultGenerator().opts(config.toClientOptInput()).generate()
    }
}

clean.doFirst {
    delete("${projectDir}/$apiTargetFolder")
}

sourceSets {
    generated {
        compileClasspath = configurations.generatedCompile
    }
    main {
        compileClasspath += generated.output
        runtimeClasspath += generated.output
    }
    test {
        compileClasspath += generated.output
        runtimeClasspath += generated.output
    }
}

compileGeneratedJava.dependsOn generateApi
classes.dependsOn generatedClasses
compileJava.dependsOn compileGeneratedJava

bootRun {
    classpath += sourceSets.generated.output
}

jar {
    from sourceSets.generated.output
}

ideaModule.dependsOn generateApi
